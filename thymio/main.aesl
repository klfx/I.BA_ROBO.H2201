<!DOCTYPE aesl-source>
<network>


<!--list of global events-->


<!--node USB Serial Device-->
<node name="USB Serial Device" nodeId="{f9a8ab75-bcc5-42dc-8fee-9c84d9c510fd}"><![CDATA[#I.BA_ROBO.H2201
#v1.5 25/10/2022


#Runtime Parameters (Feel free to change)
var STOP_IF_OFFTRACK = 0
var MAX_SPEED = 500
var THRESHOLD = 2

#Internal Variables
var status = 0
var timercount = 0
var kreuzung_active = 0
var offtrack_active = 0
var offtrack_new = 0
var amount_corrections = 0
var amount_corrections_reset = 0
timer.period[0]=500

onevent button.forward
	if button.forward == 1 then
		if  status ==0 then
				offtrack_active = 1
				status=1
		else
				offtrack_active = 0
				status=0
		end

	end

onevent prox
	if  offtrack_active == 0 then
		call math.min(offtrack_new,prox.ground.delta[0],prox.ground.delta[1])
		if  abs(prox.ground.delta[0]-prox.ground.delta[1]) > ((prox.ground.delta[0]+prox.ground.delta[1])/2 * THRESHOLD/10) and kreuzung_active == 0 then
			if  prox.ground.delta[0]>prox.ground.delta[1] then
				#LED ring right
				leds.circle[2] = 32
				leds.circle[6] = 0
			
				motor.left.target = MAX_SPEED
				motor.right.target = MAX_SPEED - 175

							
			amount_corrections += 1     
			else
				#LED ring left
				leds.circle[6] = 32
				leds.circle[2] = 0
			
				motor.left.target = MAX_SPEED - 175
				motor.right.target = MAX_SPEED

							
				amount_corrections += 1
			end
				
						
		elseif (((prox.ground.delta[0]+prox.ground.delta[1])/2) < 100) and kreuzung_active == 0 then
			#kreuzung
			timercount=0
			kreuzung_active=1
			motor.left.target = MAX_SPEED
			motor.right.target = MAX_SPEED
			leds.top[1] = 32
				
							
		elseif kreuzung_active==1 and timercount>=20 then
			kreuzung_active=0
			leds.top[1] = 0
				
		#offtrack
		elseif offtrack_new  > 870 and STOP_IF_OFFTRACK == 1 then
			offtrack_active = 1
			leds.top[2] = 32
			
		else
			#drive straight
			motor.left.target = MAX_SPEED
			motor.right.target = MAX_SPEED
			leds.circle[6] = 32
			leds.circle[2] = 32
		end
	else
		motor.left.target = 0
		motor.right.target = 0
	end
	
onevent timer0
	timercount+=5
	amount_corrections_reset+=5
	
	#Speed Control MAX Speed
	if amount_corrections_reset > 10 then
		MAX_SPEED = abs(-15 * 2 * amount_corrections + 530)
		amount_corrections_reset = 0
		amount_corrections = 0
	end
		
]]></node>


</network>
